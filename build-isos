#! /usr/bin/env perl

# Build ISO files starting last ISO file

use v5.10;
use warnings;
use strict;
use FindBin qw/$RealBin/;
use File::Spec::Functions qw/catfile/;
use local::lib catfile($RealBin, 'support');
use lib catfile($RealBin, 'lib');
use LazyScan qw/scanhome chunk_batches batch_size/;
use LazyScan::Locking qw/with_dir_locked/;
use List::Util qw/min max sum/;
use autodie;
use File::Slurp qw/read_file write_file/;

my $mkiso = catfile($RealBin, 'mkiso');
my $scanhome = scanhome();
my $inbox = catfile($scanhome, 'inbox');

my $options = get_options();
my $last_iso_path = catfile($scanhome, '.last-iso-batch');

# Approximate sizing for CD, allowing for overhead
my $maxsize = 650e6;
my $last_minsize = 550e6;

with_dir_locked(
    dir => $scanhome,
    do => sub { build_chunked_isos() },
);

sub build_chunked_isos {
    my $start = $options->{start} // do {
        my @lines = read_file($last_iso_path, { chomp => 1 });
        $lines[0] + 1
    };
    say "\$start = $start" if $options->{debug};
    my @chunks = chunk_batches($inbox, $maxsize, $start);
    return unless @chunks;

    if (sum(map { batch_size($inbox, $_) } @{$chunks[-1]}) <= $last_minsize) {
        pop @chunks;
    }
    unless (@chunks) {
	say STDERR "$0: Warning: no chunks large enough for minimum size: $last_minsize";
	return;
    }

    build_iso($_) foreach @chunks;
}

sub build_iso {
    my ($chunk) = @_;
    my $start = min @$chunk;
    my $end = max @$chunk;
    my $range = "$start-$end";
    my @cmd = ($mkiso, $range);
    say STDERR "@cmd" if $options->{debug};
    my $ret = system(@cmd);
    if ($ret != 0) {
        die "@cmd failed with exit status $ret\nSkipping remaining chunks\n";
    }
    write_file($last_iso_path, { atomic => 1 }, "$end\n") unless $options->{start};
}

sub get_options {
    use File::Basename qw(basename);
    use Getopt::Long;
    use Pod::Usage;

    my $prog = basename($0);  # can be used in error messages

    my $options = {
        help => 0,
        man => 0,
        start => undef,
        debug => undef,
    };

    Getopt::Long::Configure(qw{gnu_getopt});
    my $parsed_ok = GetOptions(
        'h|help'    =>  \$options->{help},
        'man'       =>  \$options->{man},
        'start|s=i' =>  \$options->{start},
        'debug'     =>  \$options->{debug},
    );

    pod2usage(-exitval => 2) unless $parsed_ok;

    # Note -output defaults to *STDOUT if -exitval is 0 or 1.
    # See the documentation for Pod::Usage under DESCRIPTION.
    pod2usage(-exitval => 1, -verbose => 1) if $options->{help};
    pod2usage(-exitval => 1, -verbose => 2) if $options->{man};

    if (defined $options->{start}) {
        pod2usage(-exitval => 2) unless $options->{start} >= 1;
    }

    return $options;
}

__END__

=head1 NAME

build-isos - BRIEF_DESCRIPTION

=head1 SYNOPSIS

build-isos [options]

 Options:
    -h, --help              brief help message
    --man                   full documentation

=head1 OPTIONS

=over 8

=item B<--help> or B<-h>

Print a brief help message and exit.

=item B<--man>

Print the manual page and exit.


=back

=head1 DESCRIPTION

B<build_isos> will ...

=cut
