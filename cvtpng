#!/usr/bin/perl

use v5.10;
use strict;
use warnings;

use File::Spec;
use FindBin qw/$RealBin/;
use lib File::Spec->catfile($RealBin, 'lib');

use File::Basename;

use LazyScan qw/scandir/;

use autodie qw/:io/;

unless (@ARGV) {
    say STDERR "No files listed on command line.  Exiting.";
    exit 0;
}

my @files = grep { $_ !~ /\.png/ } @ARGV;

unless (@files) {
    say STDERR "No non-PNG files to work on.  Exiting.";
    exit 0;
}

my $root = scandir();

my $converted_dir = File::Spec->catdir($root, 'converted');
mkdir $converted_dir unless -d $converted_dir;

foreach my $file (@files) {
    my $origdest = File::Spec->catfile($converted_dir, basename($file));
    my $newfile = ($file =~ s/\.[^.]+$/.png/r);
    say STDERR "CONVERT $file  -->  $newfile";
    system('convert', $file, $newfile) == 0  or next;
    say STDERR "RENAME $file  ==> $origdest";
    system 'mv', $file, $origdest;
}
